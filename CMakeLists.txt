cmake_minimum_required(VERSION 3.19.0)
project(NamelessWindow CXX)

set(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${PROJECT_SOURCE_DIR}/build/${CMAKE_BUILD_TYPE}/bin")
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${PROJECT_SOURCE_DIR}/build/${CMAKE_BUILD_TYPE}/bin")

option(BUILD_NLSWIN_EXAMPLES "ON if you want to build examples, OFF to just build the shared library" OFF)
option(BUILD_NLSWIN_DOCUMENTATION "Build doxygen docs" ON)
option(BUILD_NLSWIN_FOR_WAYLAND "ON if you wish the library to target Wayland, off if you want to target X11" OFF)


if (${BUILD_NLSWIN_DOCUMENTATION})
   find_package(Doxygen)
   if (DOXYGEN_FOUND)
      set(DOXYGEN_IN ${CMAKE_CURRENT_SOURCE_DIR}/Doxyfile)
      set(DOXYGEN_OUT ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile)
      configure_file(${DOXYGEN_IN} ${DOXYGEN_OUT} @ONLY)
      add_custom_target( doc_doxygen ALL COMMAND ${DOXYGEN_EXECUTABLE} ${DOXYGEN_OUT} WORKING_DIRECTORY 
      ${CMAKE_CURRENT_BINARY_DIR})
   endif()
endif()


set(NLSWIN_LIBRARIES_TO_LINK "")
set(NLSWIN_THIRDPARTY_INCLUDES "${PROJECT_SOURCE_DIR}/Vendor/")
set(NLSWIN_PLATFORMSPECIFIC_INCLUDES "")

if (UNIX)
   if (APPLE)
      message(FATAL_ERROR "Apple systems not currently supported.")
   else()
      if (${BUILD_NLSWIN_FOR_WAYLAND})
         # TODO: Wayland
         set(NLSWIN_WAYLAND ON)
      else()
         include (${CMAKE_ROOT}/Modules/FindX11.cmake)
         if (${X11_xcb_FOUND})
            list(APPEND NLSWIN_LIBRARIES_TO_LINK xcb-randr)
            list(APPEND NLSWIN_LIBRARIES_TO_LINK xcb-xinput)
            list(APPEND NLSWIN_LIBRARIES_TO_LINK xcb-xfixes)
            list(APPEND NLSWIN_LIBRARIES_TO_LINK xcb-icccm)
            list(APPEND NLSWIN_LIBRARIES_TO_LINK ${X11_xkbcommon_LIB})
            list(APPEND NLSWIN_LIBRARIES_TO_LINK ${X11_xkbcommon_X11_LIB})
            list(APPEND NLSWIN_LIBRARIES_TO_LINK ${X11_X11_xcb_LIB})
            list(APPEND NLSWIN_LIBRARIES_TO_LINK ${X11_X11_LIB})
            list(APPEND NLSWIN_PLATFORMSPECIFIC_INCLUDES ${X11_xcb_INCLUDE_PATH})
            set(NLSWIN_X11 ON)
         else()
            message(FATAL_ERROR "Could not find xcb library for X11!")
         endif()
      endif()
   endif()
elseif(WIN32)
   # TODO: Windows
   set(NLSWIN_WIN32)
endif()

add_subdirectory("${PROJECT_SOURCE_DIR}/src")
if (${BUILD_NLSWIN_EXAMPLES})
   add_subdirectory("${PROJECT_SOURCE_DIR}/examples/")
endif()